@namespace Platform.Communication.Protocol.Lino
@classname Parser
links <IList<Link>> = _ list:linkAndWhitespace* { list }
linkAndWhitespace <Link> = l:anyLink _ { l }
identityOrLink <Link> = l:multiLineAnyLink { l } / i:identity { i }
anyLink <Link> = multiLineAnyLink / singleLineAnyLink
multiLineAnyLink <Link> = multiLinePointLink / multiLineValueLink / multiLineLink
singleLineAnyLink <Link> = singleLineLink / singleLineValueLink / singleLinePointLink 
multiLineValueAndWhitespace <Link> = value:identityOrLink _ { value }
multiLineValues <IList<Link>> = _ list:multiLineValueAndWhitespace+ { list }
singleLineValueAndWhitespace <Link> = value:identityOrLink __ { value }
singleLineValues <IList<Link>> = __ list:singleLineValueAndWhitespace+ { list }
singleLineLink <Link> = __ id:(identity) __ ":" v:singleLineValues { new Link(id, v) }
multiLineLink <Link> = "(" _ id:(identity) _ ":" v:multiLineValues ")" { new Link(id, v) }
singleLineValueLink <Link> = v:singleLineValues { new Link(v) }
multiLineValueLink <Link> = "(" v:multiLineValues ")" { new Link(v) }
pointLink <Link> = id:(identity) { new Link(id) }
singleLinePointLink<Link> = __ l:pointLink __ { l }
multiLinePointLink<Link> = "(" _ l:pointLink _ ")" { l }
identity <string> = "" [0-9a-zA-Zа-яёА-ЯЁ]+
__ = [ \t]*
_ = [ \t\n\r]*